set(CMAKE_SYSTEM_NAME Generic)

# 工具链文件必须在CMake检测编译器之前设置编译器
# 强制使用SDCC编译器，即使CMake已经检测到其他编译器
find_program(SDCC_COMPILER sdcc REQUIRED)
set(CMAKE_C_COMPILER "${SDCC_COMPILER}" CACHE FILEPATH "SDCC Compiler" FORCE)

# 禁用CMake的编译器检测（工具链文件已指定编译器）
set(CMAKE_C_COMPILER_WORKS TRUE CACHE INTERNAL "")
set(CMAKE_C_COMPILER_ID "SDCC" CACHE INTERNAL "")

# 获取SDCC的安装目录
get_filename_component(SDCC_LOCATION "${CMAKE_C_COMPILER}" DIRECTORY)

# 查找SDCC相关工具
find_program(CMAKE_OBJCOPY sdobjcopy PATHS "${SDCC_LOCATION}" NO_DEFAULT_PATH)
find_program(CMAKE_OBJCOPY sdobjcopy)
set(CMAKE_OBJCOPY "${CMAKE_OBJCOPY}" CACHE INTERNAL "objcopy tool")

find_program(CMAKE_PACKIHX packihx PATHS "${SDCC_LOCATION}" NO_DEFAULT_PATH)
find_program(CMAKE_PACKIHX packihx)
set(CMAKE_PACKIHX "${CMAKE_PACKIHX}" CACHE INTERNAL "packihx tool")

set(CMAKE_STATIC_LIBRARY_PREFIX "")
set(CMAKE_STATIC_LIBRARY_SUFFIX ".lib")
set(CMAKE_SHARED_LIBRARY_PREFIX "")
set(CMAKE_SHARED_LIBRARY_SUFFIX ".lib")
set(CMAKE_IMPORT_LIBRARY_PREFIX )
set(CMAKE_IMPORT_LIBRARY_SUFFIX )
set(CMAKE_EXECUTABLE_SUFFIX ".ihx")
set(CMAKE_LINK_LIBRARY_SUFFIX ".lib")
set(CMAKE_DL_LIBS "")
set(CMAKE_C_OUTPUT_EXTENSION ".rel")

# 查找sdcclib工具
find_program(SDCCLIB_EXECUTABLE sdcclib PATHS "${SDCC_LOCATION}" NO_DEFAULT_PATH)
find_program(SDCCLIB_EXECUTABLE sdcclib)
if(SDCCLIB_EXECUTABLE)
    set(CMAKE_AR "${SDCCLIB_EXECUTABLE}" CACHE FILEPATH "The sdcc librarian" FORCE)
else()
    message(WARNING "sdcclib not found, static library creation may fail")
endif()

if(NOT DEFINED CMAKE_C_FLAGS_INIT)
    set(CMAKE_C_FLAGS_INIT "-mstm8 --std-c99")
endif()

if(NOT DEFINED CMAKE_EXE_LINKER_FLAGS_INIT)
    set (CMAKE_EXE_LINKER_FLAGS_INIT "")
endif()

set(CMAKE_C_COMPILE_OBJECT  "<CMAKE_C_COMPILER> <DEFINES> <INCLUDES> <FLAGS> -o <OBJECT> -c <SOURCE>")
set(CMAKE_C_LINK_EXECUTABLE "<CMAKE_C_COMPILER> <FLAGS> <OBJECTS> --out-fmt-ihx -o  <TARGET> <CMAKE_C_LINK_FLAGS> <LINK_FLAGS> <LINK_LIBRARIES>")

set(CMAKE_C_CREATE_STATIC_LIBRARY
        "\"${CMAKE_COMMAND}\" -E remove <TARGET>"
        "<CMAKE_AR> -a <TARGET> <LINK_FLAGS> <OBJECTS> ")

set(CMAKE_C_CREATE_SHARED_LIBRARY "")
set(CMAKE_C_CREATE_MODULE_LIBRARY "")

add_definitions(-D__SDCC__)